import org.apache.tools.ant.filters.EscapeUnicode
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "5.1.0"
    id "org.sonarqube" version "2.6.1"
    id "org.owasp.dependencycheck" version "5.1.1"
}

apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'java-library'
apply plugin: 'jacoco'


def externalFileName = "$projectDir/.external"

if (file(externalFileName).exists()) {
    apply from: "external.gradle"
} else {
    apply from: "monext.gradle"
}

description = 'payment-method-oney'
group = 'com.payline.payment.oney'
version = '1.5.0.4-SNAPSHOT'

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

ext {
    paymentMethodApiVersion = '1.9'
    paymentMethodIntegrationVersion = '1.4'
    mockitoVersion = '2.19.0'
    releaseDirectoryPath = 'D:/build'
}

configurations {
    fatJarTest
}

dependencies {
    compileOnly("com.payline:payment-method-api:$paymentMethodApiVersion")

    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.3'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    implementation group: 'org.apache.httpcomponents', name: 'httpcore', version: '4.4.13'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'

    compileOnly group: 'javax.xml.ws', name: 'jaxws-api', version: '2.3.1'
    compileOnly group: 'javax.jws', name: 'javax.jws-api', version:  '1.1'
    compileOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    testRuntimeOnly group: 'javax.xml.ws', name: 'jaxws-api', version: '2.3.1'
    testRuntimeOnly group: 'javax.jws', name: 'javax.jws-api', version:  '1.1'
    testRuntimeOnly group: 'javax.annotation', name: 'javax.annotation-api', version: '1.3.2'

    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.13.3'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.13.3'

    testImplementation group: 'org.jmockit', name: 'jmockit', version: '1.49'

    testImplementation "com.payline:payment-method-api:${paymentMethodApiVersion}"
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.7'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2'

    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.141.59'
    testImplementation group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '3.141.59'

    implementation 'org.apache.commons:commons-lang3:3.11'

    // fatJarTest include testImplementation dependencies (not for IT)
    fatJarTest group: 'org.powermock', name: 'powermock-api-mockito2', version: '2.0.7'
    fatJarTest group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.2'
    fatJarTest group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: '5.6.2'
    fatJarTest group: 'org.seleniumhq.selenium', name: 'selenium-server', version: '3.141.59'
    fatJarTest group: 'org.seleniumhq.selenium', name: 'selenium-support', version: '3.141.59'
}

dependencyCheck {
    format 'ALL'
    skipConfigurations = ['jacocoAgent', 'jacocoAnt', 'annotationProcessor', 'axisGenAntTask', 'jaxws', 'execJar', 'jaxb', 'generateSources']
}

assemble.dependsOn shadowJar

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency('com.payline:payment-method-api'))
        exclude(dependency('org.apache.logging.log4j:log4j-api:2.13.3'))
        exclude(dependency('org.apache.logging.log4j:log4j-core:2.13.3'))
        exclude(dependency('org.junit.jupiter:junit-jupiter-api:5.6.2'))
        exclude(dependency('org.powermock:powermock-api-mockito2:2.0.7'))
        exclude(dependency('org.jmockit:jmockit:1.49'))
        exclude(dependency('org.powermock:powermock-module-junit4:2.0.7'))
        exclude(dependency('org.powermock:powermock-module-junit4-rule-agent:2.0.7'))
        exclude(dependency('org.powermock:powermock-module-junit4-rule: 2.0.7'))
        exclude(dependency('org.seleniumhq.selenium:selenium-server:3.141.59'))
        exclude(dependency('org.seleniumhq.selenium:selenium-support:3.141.59'))
    }
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Build-Date': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'Source-Compatibility': project.sourceCompatibility,
                'Target-Compatibility': project.targetCompatibility
        )
    }
}

task buildToLocal(type: Copy) {
    dependsOn shadowJar
    from shadowJar
    into "${releaseDirectoryPath}"
}

task printVersion {
    doLast {
        println version
    }
}

/* Replace tokens in resources files after compilation */
processResources {
    filesMatching('messages_fr.properties') {
        filter(EscapeUnicode)
    }
    filesMatching('release.properties') {
        filter(ReplaceTokens, tokens: [
                'version': project.version.replaceFirst("-.*", ""),
                'date'   : new Date().format("dd/MM/yyyy")
        ])
    }
}

// To create a JAR including test classes and dependencies
task testJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
    dependsOn 'shadowJar', 'testClasses'
    classifier = 'tests'
    from sourceSets.test.output.classesDirs + shadowJar
    configurations = [project.configurations.fatJarTest]
}

test {
    useJUnitPlatform()
    exclude('**/*IT.class')
    jvmArgs "-javaagent:${classpath.find { it.name.contains("jmockit") }.absolutePath}"
}
